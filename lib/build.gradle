apply plugin: 'android-library'
apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = 'android-crop'

android {
    compileSdkVersion 19
    buildToolsVersion '19.1.0'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19

        testApplicationId 'com.soundcloud.android.crop.test'
        testInstrumentationRunner 'android.test.InstrumentationTestRunner'
    }
}

dependencies {
    androidTestCompile 'com.squareup:fest-android:1.0.7'
    androidTestCompile 'com.android.support:support-v4:19.1.0'
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
    androidTestCompile 'com.google.dexmaker:dexmaker:1.0'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.0'
}

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives

    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Android Crop'
                packaging 'aar'
                description 'An Android library that provides an image cropping Activity'
                url 'https://github.com/jdamcd/android-crop'

                scm {
                    url 'scm:git@github.com:jdamcd/android-crop.git'
                    connection 'scm:git@github.com:jdamcd/android-crop.git'
                    developerConnection 'scm:git@github.com:jdamcd/android-crop.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                organization {
                    name 'SoundCloud'
                    url 'http://developers.soundcloud.com'
                }

                developers {
                    developer {
                        id 'jdamcd'
                        name 'Jamie McDonald'
                        email 'mcdonald@soundcloud.com'
                    }
                }
            }
        }
    }
}

